apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "pl.mareklangiewicz.myhub"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        debug {
            storeFile file('keystore/open.keystore.jks')
            keyAlias 'openkey'
            storePassword 'openkey'
            keyPassword 'openkey'
        }
        release {
            // TODO SOMEDAY: different config for release
            storeFile file('keystore/open.keystore.jks')
            keyAlias 'openkey'
            storePassword 'openkey'
            keyPassword 'openkey'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        warning 'InvalidPackage'
    }
    resourcePrefix 'mh_'

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}

kapt {
    generateStubs = true
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.27'
//    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
//    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.github.langara.MyBlocks:myactivities:1.0.2-alpha'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-moshi:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'
//    compile 'com.jakewharton.rxbinding:rxbinding-kotlin:0.3.0' TODO LATER: use this when RxBinding is updated to new Kotlin
    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    compile 'com.jakewharton.rxrelay:rxrelay:1.0.0'
    compile 'com.squareup.okhttp:logging-interceptor:2.6.0'
    compile 'com.squareup.picasso:picasso:2.5.2'

    kapt 'com.google.dagger:dagger-compiler:2.0.2'
    compile 'com.google.dagger:dagger:2.0.2'

    compile "io.realm:realm-android-library:$realm_version@aar"
    compile "io.realm:realm-annotations:$realm_version"
    kapt "io.realm:realm-annotations:$realm_version"
    kapt "io.realm:realm-annotations-processor:$realm_version"


    provided 'javax.annotation:jsr250-api:1.0'
    compile('com.afollestad.material-dialogs:core:0.8.4.2@aar') {
        transitive = true
    }
    compile('com.afollestad.material-dialogs:commons:0.8.4.2@aar') {
        transitive = true
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'hu.supercluster:paperwork:1.2.2'
}
buildscript {
    ext.kotlin_version = '1.0.0-rc-1036'
    ext.realm_version = '0.87.4'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath 'hu.supercluster:paperwork-plugin:1.2.2'
    }
}
apply plugin: 'hu.supercluster.paperwork'
paperwork {
    set = [
        buildTime: buildTime("yyyy-MM-dd HH:mm:ss"),
        gitSha: gitSha(),
        gitTag: gitTag(),
        gitInfo: gitInfo()
    ]
}
